module message

struct MessagePayloadMint {
  token_name: ByteArray
  quantity: Int
  recipient_address: Address
}

struct MessagePayloadBurn {
  token_name: ByteArray
  quantity: Int
  sender_pkh: PubKeyHash
}

// Following Hyperlane as tight as possible
// https://github.com/hyperlane-xyz/hyperlane-monorepo/blob/main/solidity/contracts/libs/Message.sol
// "Int" is unbounded in Plutus so we use "ByteArray" to easier handle padding.
// TODO: More specific types for addresses, etc.
struct Message {
  // The version of the origin and destination Mailboxes
  version: ByteArray
  // A nonce to uniquely identify the message on its origin chain
  nonce: ByteArray
  // Domain of origin chain
  origin_domain: ByteArray
  // Address of sender as bytes32
  // A credential in Cardano is 28 bytes (Blake2b_224),
  // We pad 4 bytes on the left:
  // 00000000: Payment credential that must sign the tx.
  // 01000000: Minting policy hash that must mint in the tx.
  sender: ByteArray
  // Domain of destination chain
  destination_domain: ByteArray
  // Address of recipient on destination chain as bytes32
  recipient: ByteArray
  // Raw bytes of message body
  message: ByteArray

  func is_valid_outbound(self, origin_domain: ByteArray) -> Bool {
    self.version == #00 &&
    self.nonce.length == 4 &&
    self.origin_domain == origin_domain &&
    self.sender.length == 32 &&
    self.destination_domain.length == 4 &&
    self.recipient.length == 32 &&
    self.destination_domain != self.origin_domain
  }

  func id(self) -> ByteArray {
    ( self.version +
      self.nonce +
      self.origin_domain +
      self.sender +
      self.destination_domain +
      self.recipient +
      self.message
    ).blake2b()
  }

  func get_recipient(self) -> Address {
    Address::new(
      // TODO: Support pubkey address
      Credential::new_validator(ValidatorHash::new(self.recipient.slice(4, 32))),
      // TODO: Support staking credential
      Option[StakingCredential]::None
    )
  }

  // TODO: Support multiple asset mints at once
  // Assuming minting only one asset at the moment.
  func get_mint_payload(self) -> MessagePayloadMint {
    recipient_address_hash: ByteArray = self.message.slice(96, 128);
    recipient_address_length: Int = Int::from_big_endian(self.message.slice(256, 288));
    recipient_address_raw: ByteArray = self.message.slice(288, 288 + recipient_address_length);
    assert(recipient_address_raw.blake2b() == recipient_address_hash, "invalid hash");
    recipient_address: Address = Address::new(
      // TODO: Support validator address
      Credential::new_pubkey(PubKeyHash::new(recipient_address_raw.slice(1, 29))),
      if (recipient_address_raw.length > 29) {
        // TODO: Support stake validator
        Option[StakingCredential]::Some{
          StakingCredential::new_hash(StakingHash::new_stakekey(StakeKeyHash::new(
            recipient_address_raw.slice(29, 57)
          )))
        }
      } else {
        Option[StakingCredential]::None
      }
    );
    token_name_length: Int = Int::from_big_endian(self.message.slice(192, 193));
    token_name: ByteArray = self.message.slice(193, 193 + token_name_length);
    quantity: Int = Int::from_big_endian(self.message.slice(224, 256));
    MessagePayloadMint {
      token_name,
      quantity,
      recipient_address
    }
  }

  // TODO: Support multiple asset burns at once
  // Assuming burning only one asset at the moment.
  func get_burn_payload(self) -> MessagePayloadBurn {
    // TODO: Truer support for ABI decode, for potentially non-empty
    // interchain liquidity hub payload, etc.
    sender_pkh: PubKeyHash = PubKeyHash::new(self.message.slice(4, 32));
    quantity: Int = Int::from_big_endian(self.message.slice(288, 320));
    token_name_length: Int = Int::from_big_endian(self.message.slice(256, 257));
    token_name: ByteArray = self.message.slice(257, 257 + token_name_length);
    MessagePayloadBurn {
      token_name,
      quantity,
      sender_pkh
    }
  }
}
